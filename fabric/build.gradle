plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    repositories {
        maven {
            url "https://cursemaven.com"
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    modApi "maven.modrinth:jei:11.6.0.1018"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    common(project(path: ":fabric-like", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":fabric-like", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "group", maven_group
    inputs.property "version", project.version
    inputs.property "architecturyVersion", architectury_version
    inputs.property "minecraftVersion", minecraft_version

    filesMatching("fabric.mod.json") {
        expand "group": maven_group,
                "version": version,
                "architecturyVersion": architectury_version,
                "minecraftVersion": minecraft_version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "${minecraft_version}-${fabric_suffix}"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.fabric_suffix}"
    projectId = project.modrinth_id
    versionNumber = project.mod_version
    versionType = project.fabric_release_type
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ['fabric']
    changelog = project.changelog
    dependencies {
        required.version project.fabric_modrinth_dependancies
    }
    debugMode = fabric_debug.toBoolean()
}
tasks.getByName('curseforge').dependsOn(':build', ':fabric:remapJar')
curseforge {
    apiKey = System.getenv("CURSEFORGE_API")
    project {
        id = project.curseforge_id
        changelog = project.changelog
        releaseType = project.fabric_release_type
        addGameVersion 'Fabric'
        addGameVersion project.minecraft_version
        relations {
            requiredDependency project.curseforge_dependency
        }
        mainArtifact(remapJar) {
            displayName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.fabric_suffix}"
        }

    }
    options {
        debug = project.fabric_debug.toBoolean()
    }
}