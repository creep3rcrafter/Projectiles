plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "projectiles-common.mixins.json"
        mixinConfig "projectiles.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    repositories {
        maven {
            url "https://cursemaven.com"
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modApi "maven.modrinth:jei:11.6.0.1018"
    modApi "curse.maven:mod-name-tooltip-238747:3898427"
    modApi "curse.maven:itemzoom-261725:3903160"
    //modApi "maven.modrinth:rubidium:0.6.2b"
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "group", maven_group
    inputs.property "version", project.version
    inputs.property "architecturyVersion", architectury_version
    inputs.property "minecraftVersion", minecraft_version
    inputs.property "forgeMajorVersion", forge_major_version

    filesMatching("META-INF/mods.toml") {
        expand "group": maven_group,
                "version": version,
                "architecturyVersion": architectury_version,
                "minecraftVersion": minecraft_version,
                "forgeMajorVersion": forge_major_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "${minecraft_version}-${forge_suffix}"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.forge_suffix}"
    projectId = modrinth_id
    versionNumber = project.mod_version
    versionType = project.forge_release_type
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["forge"]
    changelog = project.changelog
    dependencies {
        required.version forge_modrinth_dependancies
    }
    debugMode = project.forge_debug.toBoolean()
}
tasks.getByName('curseforge').dependsOn(':build', ':forge:remapJar')
curseforge {
    apiKey = System.getenv("CURSEFORGE_API")
    project {
        id = project.curseforge_id
        changelog = project.changelog
        releaseType = project.forge_release_type
        addGameVersion 'Forge'
        addGameVersion project.minecraft_version
        relations {
            requiredDependency project.curseforge_dependency
        }
        mainArtifact(remapJar) {
            displayName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.forge_suffix}"
        }

    }
    options {
        debug = project.forge_debug.toBoolean()
    }
}