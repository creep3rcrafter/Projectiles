plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}
loom {
    neoForge {
        accessTransformer(file('src/main/resources/META-INF/accesstransformer.cfg'))
    }
}
configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    repositories {
        maven {
            url "https://cursemaven.com"
        }
        maven { url "https://maven.neoforged.net/releases" }
        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }
            filter {
                includeGroup "maven.modrinth"
            }
        }
    }
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
    modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"
    //modApi "maven.modrinth:jei:${rootProject.jei_version}"
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    //shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

processResources {
    inputs.property "group", maven_group
    inputs.property "version", project.version
    inputs.property "architecturyVersion", architectury_version
    inputs.property "minecraftVersion", minecraft_version
    inputs.property "neoforgeMajorVersion", neoforge_major_version

    filesMatching("META-INF/mods.toml") {
        expand "group": maven_group,
                "version": version,
                "architecturyVersion": architectury_version,
                "minecraftVersion": minecraft_version,
                "neoforgeMajorVersion":  neoforge_major_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("${minecraft_version}-${neoforge_suffix}")
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.neoforge_suffix}"
    projectId = modrinth_id
    versionNumber = project.mod_version
    versionType = project.neoforge_release_type
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ["neoforge"]
    changelog = project.changelog
    dependencies {
        required.version neoforge_modrinth_dependancies
    }
    debugMode = project.neoforge_debug.toBoolean()
}
tasks.getByName('curseforge').dependsOn(':build', ':neoforge:remapJar')
curseforge {
    apiKey = System.getenv("CURSEFORGE_API")
    project {
        id = project.curseforge_id
        changelog = project.changelog
        releaseType = project.neoforge_release_type
        addGameVersion 'NeoForge'
        addGameVersion project.minecraft_version
        relations {
            requiredDependency project.curseforge_dependency
        }
        mainArtifact(remapJar) {
            displayName = "${project.archives_base_name}-${project.mod_version}-${project.minecraft_version}-${project.neoforge_suffix}"
        }

    }
    options {
        debug = project.neoforge_debug.toBoolean()
    }
}